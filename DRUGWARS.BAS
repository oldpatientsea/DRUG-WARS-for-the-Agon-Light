   10 REM >DRUGWARS.BBC V1.0 26 JULY 2023
   20 REM ORIGINAL DOS GAME (C) JOHN E DELL (1984)
   30 REM AGON LIGHT VERSION (C) TIM DELMARE (2023)
   40 ON ERROR IF ERR=17 THEN GOTO 8630
   50 PROC_CONSTANTS
   60 PROC_INITSCREEN
   70 PROC_TITLESPLASH
   80 PROC_FBIWARNING
   90 GAMEOVER=FALSE
  100 PROC_SHOW_CURSOR
  110 REPEAT
  120   PROC_OFFERINSTRUCTIONS
  130   PROC_INIT
  140   PROC_SETCURRENTPRICES
  150   PROC_MAINLOOP
  160 UNTIL GAMEOVER=TRUE
  170 PROC_RESTOREPREGAMESETTINGS
  180 END
  190 :
  200 DEF PROC_MAINLOOP
  210 REM WE HAVE 30 DAYS TO PAY OFF OUR DEBTS AND GET RICH OR DIE TRYING
  220 REPEAT
  230   REM THE DAILY GRIND. ARRIVE. IF BRONX, DO BRONX STUFF. BUY/SELL UNTIL JET
  240   IF GEO% = 1 AND JUSTARRIVED = TRUE THEN JUSTARRIVED = FALSE : PROC_BRONX
  250   CURRENTDAY% = DAY%
  260   REPEAT
  270     PROC_RENDERUI
  280     PROC_BUYSELLJET
  290     CLS
  300   UNTIL DAY% > CURRENTDAY%
  310 UNTIL DAY% = 31  OR DAMAGE% > 50
  320 GAMEOVER=TRUE
  330 CLS
  340 SCORE% = INT(2 * (CASH%+BANK%-DEBT%)/1000000)
  350 IF SCORE% <= 1 SCORE% = 1
  360 IF DAMAGE% <= 50 THEN PROC_RENDERQUESTION("CONGRATULATIONS !! ON A SCALE OF 1 TO 100, YOU RATE "+ STR$(SCORE%))
  370 PROC_FURTHERQUESTION(" ")
  380 PRINT TAB(26,25);"PLAY AGAIN ?";
  390 LOCAL PLAYAGAIN
  400 PLAYAGAIN = FALSE
  410 PLAYAGAIN = FN_DO_I_HEAR_YES
  420 IF PLAYAGAIN = TRUE THEN GAMEOVER = FALSE : CLS
  430 ENDPROC
  440 :
  450 DEF FN_HOLDCAPACITY
  460 = HOLD%-FN_TOTALHOLDING
  470 :
  480 DEF FN_TOTALHOLDING
  490 LOCAL HOLDINGTOTAL%
  500 FOR I% = 0 TO 5
  510   HOLDINGTOTAL% = HOLDINGTOTAL% + HOLDING%(I%)
  520 NEXT I%
  530 = HOLDINGTOTAL%
  540 :
  550 DEF PROC_BRONX
  560 PROC_HUD
  570 PROC_ASKVISITLOANSHARK
  580 CLS : PROC_HUD
  590 PROC_ASKTOSTASH
  600 CLS : PROC_HUD
  610 PROC_ASKTOVISITBANK
  620 CLS : PROC_HUD
  630 ENDPROC
  640 :
  650 DEF PROC_ASKVISITLOANSHARK
  660 PROC_HIDEPRICES
  670 PROC_CLEARQUESTION
  680 PROC_RENDERQUESTION("DO YOU WANT TO VISIT THE LOAN SHARK ?")
  690 LOCAL ANSWER
  700 ANSWER = FALSE
  710 ANSWER = FN_DO_I_HEAR_YES
  720 IF ANSWER = TRUE THEN PROC_REPAY
  730 ENDPROC
  740 :
  750 DEF PROC_REPAY
  760 REPEAT
  770   PROC_FURTHERQUESTION("HOW MUCH DO YOU WANT TO REPAY ")
  780   INPUT P%
  790 UNTIL P% <= CASH%
  800 CASH% = CASH% - P%
  810 DEBT%=DEBT%-P%
  820 IF DEBT% < MAXLOAN% THEN PROC_HOWMUCHTOBORROW
  830 PROC_FURTHERQUESTION("")
  840 ENDPROC
  850 :
  860 DEF PROC_HOWMUCHTOBORROW
  870 REPEAT
  880   PROC_FURTHERQUESTION("HOW MUCH DO YOU WANT TO BORROW ")
  890   INPUT B%
  900 UNTIL B% + DEBT% <= MAXLOAN%
  910 DEBT% = DEBT% + B%
  920 CASH% = CASH% + B%
  930 ENDPROC
  940 :
  950 DEF PROC_ASKTOSTASH
  960 PROC_HIDEPRICES
  970 PROC_CLEARQUESTION
  980 PROC_RENDERQUESTION("DO YOU WISH TO TRANSFER DRUGS TO YOUR STASH ?")
  990 LOCAL ANSWER
 1000 ANSWER = FALSE
 1010 ANSWER = FN_DO_I_HEAR_YES
 1020 IF ANSWER = TRUE THEN PROC_STASH
 1030 ENDPROC
 1040 :
 1050 DEF PROC_STASH
 1060 LOCAL LEGALMOVE
 1070 FOR DESIREDDRUG% = 0 TO 5
 1080   LEGALMOVE = FALSE
 1090   REPEAT
 1100     QUESTION$ = "HOW MUCH "+DRUGNAMES$(DESIREDDRUG%)+" TO STASH "
 1110     PROC_FURTHERQUESTION(QUESTION$)
 1120     INPUT STASHAMT%
 1130     IF HOLDING%(DESIREDDRUG%) >= STASHAMT% THEN PROC_STASH(DESIREDDRUG%, STASHAMT%) : LEGALMOVE = TRUE
 1140   UNTIL LEGALMOVE = TRUE
 1150   LEGALMOVE = FALSE
 1160   REPEAT
 1170     QUESTION$ = "HOW MUCH "+DRUGNAMES$(DESIREDDRUG%)+" TO TRENCHCOAT "
 1180     PROC_FURTHERQUESTION(QUESTION$)
 1190     INPUT UNSTASHAMT%
 1200     IF STASH%(DESIREDDRUG%) >= UNSTASHAMT% THEN PROC_UNSTASH(DESIREDDRUG%, UNSTASHAMT%) : LEGALMOVE = TRUE
 1210   UNTIL LEGALMOVE = TRUE
 1220 NEXT
 1230 ENDPROC
 1240 :
 1250 DEF PROC_STASH(DESIREDDRUG%, STASHAMT%)
 1260 HOLDING%(DESIREDDRUG%) =  HOLDING%(DESIREDDRUG%) - STASHAMT%
 1270 STASH%(DESIREDDRUG%) = STASH%(DESIREDDRUG%) + STASHAMT%
 1280 ENDPROC
 1290 :
 1300 DEF PROC_UNSTASH(DESIREDDRUG%, UNSTASHAMT%)
 1310 STASH%(DESIREDDRUG%) = STASH%(DESIREDDRUG%) - UNSTASHAMT%
 1320 HOLDING%(DESIREDDRUG%) = HOLDING%(DESIREDDRUG%) + UNSTASHAMT%
 1330 ENDPROC
 1340 :
 1350 DEF PROC_ASKTOVISITBANK
 1360 PROC_HIDEPRICES
 1370 PROC_CLEARQUESTION
 1380 PROC_RENDERQUESTION("DO YOU WISH TO VISIT THE BANK ?")
 1390 LOCAL ANSWER
 1400 ANSWER = FALSE
 1410 ANSWER = FN_DO_I_HEAR_YES
 1420 IF ANSWER = TRUE THEN PROC_BANK
 1430 ENDPROC
 1440 :
 1450 DEF PROC_BANK
 1460 LOCAL D%, W%
 1470 REPEAT
 1480   PROC_FURTHERQUESTION("HOW MUCH DO YOU WANT TO DEPOSIT ")
 1490   INPUT  D%
 1500 UNTIL D% <= CASH%
 1510 CASH% = CASH% - D%
 1520 BANK% = BANK% + D%
 1530 REPEAT
 1540   PROC_FURTHERQUESTION("HOW MUCH DO YOU WANT TO WITHDRAW ")
 1550   INPUT W%
 1560 UNTIL W% <= BANK%
 1570 CASH% = CASH% + W%
 1580 BANK% = BANK% - W%
 1590 PROC_FURTHERQUESTION("")
 1600 ENDPROC
 1610 :
 1620 DEF PROC_BUYSELLJET
 1630 LOCAL KEY%, LEGALKEY
 1640 PROC_CLEARQUESTION
 1650 PROC_RENDERQUESTION("WILL YOU BUY, SELL OR JET ?")
 1660 LEGALKEY = FALSE
 1670 KEY% = -1
 1680 REPEAT
 1690   REPEAT
 1700     KEY%=INKEY(10000)
 1710   UNTIL KEY% <> -1
 1720   IF (KEY% = ASC("B")) OR (KEY% = ASC("b")) THEN LEGALKEY = TRUE : PROC_BUY
 1730   IF (KEY% = ASC("S")) OR (KEY% = ASC("s")) THEN LEGALKEY = TRUE : PROC_SELL
 1740   IF (KEY% = ASC("J")) OR (KEY% = ASC("j")) THEN LEGALKEY = TRUE : PROC_JET
 1750 UNTIL LEGALKEY = TRUE
 1760 ENDPROC
 1770 :
 1780 DEF PROC_BUY
 1790 LOCAL KEY%, DESIREDDRUG%
 1800 DESIREDDRUG% = -1
 1810 KEY% = -1
 1820 PROC_FURTHERQUESTION("WHAT WILL YOU BUY ?")
 1830 DESIREDDRUG% = -1
 1840 REPEAT
 1850   REPEAT
 1860     KEY%=INKEY(10000)
 1870   UNTIL KEY% <> -1
 1880   DESIREDDRUG% = FN_DESIREDDRUG(KEY%)
 1890   IF DESIREDDRUG% <> -1 THEN PROC_BUY(DESIREDDRUG%)
 1900 UNTIL DESIREDDRUG% <> -1
 1910 ENDPROC
 1920 :
 1930 DEF PROC_BUY(DRUG%)
 1940 MAXAFFORD% = CASH%/CURRENTPRICES%(DRUG%)
 1950 LIMIT$ = "YOU CAN AFFORD ( "+STR$(MAXAFFORD%)+" )"
 1960 QUESTION$ = "HOW MUCH "+DRUGNAMES$(DRUG%)+" DO YOU WANT TO BUY "
 1970 PROC_FURTHERQUESTIONEXTRA(LIMIT$)
 1980 PROC_FURTHERQUESTION(QUESTION$)
 1990 INPUT QUANTITY%
 2000 IF QUANTITY% <= MAXAFFORD% AND QUANTITY% <= FN_HOLDCAPACITY THEN PROC_INCREASEINVENTORY(DRUG%, QUANTITY%)
 2010 ENDPROC
 2020 :
 2030 REM UPDATE INVENTORY AND MONEY
 2040 REM ALREADY VALIDATED IT CAN BE AFFORDED
 2050 DEF PROC_INCREASEINVENTORY(DRUG%, QUANTITY%)
 2060 HOLDING%(DRUG%) = HOLDING%(DRUG%) + QUANTITY%
 2070 CASH% = CASH% - (CURRENTPRICES%(DRUG%)*QUANTITY%)
 2080 ENDPROC
 2090 :
 2100 DEF PROC_SELL
 2110 LOCAL KEY%, DESIREDDRUG%
 2120 DESIREDDRUG% = -1
 2130 KEY% = -1
 2140 PROC_FURTHERQUESTION("WHAT WILL YOU SELL ?")
 2150 REPEAT
 2160   REPEAT
 2170     KEY%=INKEY(10000)
 2180   UNTIL KEY% <> -1
 2190   DESIREDDRUG% = FN_DESIREDDRUG(KEY%)
 2200   IF DESIREDDRUG% <> -1 THEN PROC_SELL(DESIREDDRUG%)
 2210 UNTIL DESIREDDRUG% <> -1
 2220 ENDPROC
 2230 :
 2240 DEF PROC_SELL(DRUG%)
 2250 QUESTION$ = "HOW MUCH "+DRUGNAMES$(DRUG%)+" DO YOU WANT TO SELL "
 2260 AVAILABLE$ = "YOU CAN SELL ( "+STR$(HOLDING%(DRUG%))+" )"
 2270 PROC_FURTHERQUESTIONEXTRA(AVAILABLE$)
 2280 PROC_FURTHERQUESTION(QUESTION$)
 2290 INPUT SELL%
 2300 IF SELL% > 0 AND SELL% <= HOLDING%(DRUG%) THEN PROC_REDUCEINVENTORY(DRUG%, SELL%)
 2310 ENDPROC
 2320 :
 2330 REM HAVE ALREADY VALIDATED LEGAL QUANTITIES
 2340 DEF PROC_REDUCEINVENTORY(DRUG%,SELL%)
 2350 HOLDING%(DRUG%) = HOLDING%(DRUG%) - SELL%
 2360 CASH% = CASH% + (CURRENTPRICES%(DRUG%)*SELL%)
 2370 ENDPROC
 2380 :
 2390 DEF PROC_JET
 2400 LOCAL DESTINATION$
 2410 PROC_HIDEPRICES
 2420 PROC_CLEARQUESTION
 2430 PRINT TAB(LEFT%+5,22)"(1) BRONX     (2) GHETTO       (3) CENTRAL PARK"
 2440 PRINT TAB(LEFT%+5,23)"(4) MANHATTAN (5) CONEY ISLAND (6) BROOKLYN"
 2450 PRINT TAB(LEFT%,20)"WHERE TO DUDE ";
 2460 REPEAT
 2470   DESTINATION$=GET$
 2480 UNTIL DESTINATION$ >="1" AND DESTINATION$ <="6" AND VAL(DESTINATION$) <> GEO%
 2490 GEO% = VAL(DESTINATION$)
 2500 PROC_NEWDAY
 2510 ENDPROC
 2520 :
 2530 DEF PROC_NEWDAY
 2540 CLS
 2550 DAY% = DAY% + 1
 2560 JUSTARRIVED = TRUE
 2570 DEBT% = DEBT%*110/100
 2580 BANK% = BANK%*105/100
 2590 PROC_SETCURRENTPRICES
 2600 PROC_RANDOMFIGHT
 2610 IF DAMAGE% <= 50 THEN PROC_RANDOMEVENT
 2620 ENDPROC
 2630 :
 2640 DEF PROC_RANDOMFIGHT
 2650 REM PROBABILITY BASED ON MULTIPLE PLAYTHROUGHS OF ORIGINAL
 2660 REM VALUE OF DRUGS DOES NOT SEEM TO MATTER. QUANTITY DOES.
 2670 LOCAL HARDASS, HEAT%, CARRYING%
 2680 HARDASS = FALSE
 2690 CARRYING% = FN_TOTALHOLDING
 2700 HEAT = 3
 2710 IF CARRYING% >= 10 THEN HEAT = HEAT * CARRYING%/10
 2720 IF RND(100) < HEAT THEN HARDASS = TRUE
 2730 COPS% = RND(3)
 2740 IF HARDASS THEN PROC_CHASE
 2750 ENDPROC
 2760 :
 2770 DEF PROC_CHASE
 2780 CLS
 2790 MESSAGE$ = "OFFICER HARDASS AND "+STR$(COPS%)+" OF HIS DEPUTIES ARE CHASING YOU !!"
 2800 PROC_DISPLAYEVENT(MESSAGE$)
 2810 PROC_WAITFORSPACE
 2820 CLS
 2830 PROC_FIGHT
 2840 ENDPROC
 2850 :
 2860 DEF PROC_FIGHT
 2870 LOCAL ESCAPE
 2880 ESCAPE = FALSE
 2890 REPEAT
 2900   PROC_FIGHTHUD
 2910   PROC_SHOW_CURSOR
 2920   IF GUNS% = 0 THEN ESCAPE = FN_WILLYOURUN ELSE ESCAPE = FN_FIGHTORRUN
 2930   PROC_HIDE_CURSOR
 2940   IF ESCAPE = FALSE THEN PROC_FIRINGONYOU
 2950   IF ESCAPE = FALSE AND GUNS% > 0 THEN PROC_FIRINGONTHEM
 2960 UNTIL ESCAPE = TRUE OR DAMAGE% > 50 OR COPS% = 0
 2970 IF ESCAPE = TRUE THEN PROC_FIGHTSTATUSLEFT("YOU LOST THEM IN THE ALLEYS !!") : PROC_BEAT
 2980 IF DAMAGE% > 50 THEN PROC_FIGHTSTATUSLEFT("THEY WASTED YOU MAN !! WHAT A DRAG !!!") : PROC_BEAT : PROC_BEAT
 2990 IF COPS% = 0 THEN PROC_FIGHTSTATUSLEFT("YOU KILLED ALL OF THEM !!!!") : PROC_BEAT : PROC_FIGHTREWARD
 3000 PROC_SHOW_CURSOR
 3010 ENDPROC
 3020 :
 3030 DEF FN_WILLYOURUN
 3040 LOCAL ESCAPE, TRYTORUN
 3050 ESCAPE = FALSE
 3060 TRYTORUN = FALSE
 3070 PROC_FIGHTQUESTION("WILL YOU RUN ?")
 3080 LOCAL ANSWER
 3090 ANSWER = FALSE
 3100 ANSWER = FN_DO_I_HEAR_YES
 3110 IF ANSWER = TRUE THEN TRYTORUN = TRUE
 3120 IF TRYTORUN= TRUE THEN ESCAPE = (RND(2) = 1) : REM 50% CHANCE OF ESCAPE
 3130 IF TRYTORUN = TRUE AND ESCAPE = FALSE THEN PROC_FIGHTSTATUSLEFT("YOU CAN'T LOSE THEM !!") : PROC_BEAT
 3140 PROC_SHOW_CURSOR
 3150 =ESCAPE
 3160 :
 3170 DEF FN_FIGHTORRUN
 3180 LOCAL ESCAPE, TRYTORUN
 3190 ESCAPE = FALSE
 3200 TRYTORUN = FALSE
 3210 PROC_FIGHTQUESTION("WILL YOU FIGHT OR RUN ?")
 3220 REPEAT
 3230   KEY%=INKEY(10000)
 3240 UNTIL KEY% <> -1 AND KEY% = ASC("F") OR KEY% = ASC("f") OR KEY% = ASC("R") OR KEY% = ASC("r")
 3250 PROC_HIDE_CURSOR
 3260 IF (KEY% = ASC("R")) OR (KEY% = ASC("r")) THEN TRYTORUN = TRUE
 3270 IF TRYTORUN = TRUE THEN ESCAPE = (RND(2) = 1) : REM 50% CHANCE OF ESCAPE
 3280 IF TRYTORUN = TRUE AND ESCAPE = FALSE THEN PROC_FIGHTSTATUSLEFT("YOU CAN'T LOSE THEM !!") : PROC_BEAT
 3290 PROC_SHOW_CURSOR
 3300 =ESCAPE
 3310 :
 3320 DEF PROC_FIRINGONYOU
 3330 PROC_FIGHTSTATUSLEFT("THEY ARE FIRING ON YOU MAN !!")
 3340 PROC_BEAT
 3350 REM EACH COP HAS A 1/4 CHANCE OF HITTING YOU FOR 1-2 DAMAGE
 3360 LOCAL DAMAGEHIT%
 3370 FOR I = 1 TO COPS%
 3380   IF RND(100)<=25 THEN DAMAGEHIT% = DAMAGEHIT% + RND(2)
 3390 NEXT
 3400 IF DAMAGEHIT% <= 0 PROC_FIGHTSTATUSRIGHT("THEY MISSED !!")
 3410 IF DAMAGEHIT% > 0 THEN DAMAGE% = DAMAGE%+DAMAGEHIT% : PROC_FIGHTSTATUSRIGHT("YOU'VE BEEN HIT !!")
 3420 PROC_FIGHTHUD
 3430 PROC_BEAT : CLS : PROC_FIGHTHUD
 3440 ENDPROC
 3450 :
 3460 DEF PROC_FIRINGONTHEM
 3470 LOCAL KILLTHEM%
 3480 PROC_FIGHTSTATUSLEFT("YOU'RE FIRING ON THEM  !!") : PROC_BEAT
 3490 KILLTHEM% = RND(GUNS%*2)-1
 3500 IF KILLTHEM% = 0 THEN PROC_FIGHTSTATUSRIGHT("YOU MISSED THEM !!")
 3510 IF KILLTHEM% > 0 THEN COPS% = COPS% - 1
 3520 REM WE WILL HANDLE THE CASE THAT YOU KILLED _ALL_ OF THEM IN PROC_FIGHT
 3530 IF KILLTHEM% > 0 AND COPS% > 0 THEN PROC_FIGHTSTATUSRIGHT("YOU KILLED ONE OF THEM !!")
 3540 PROC_BEAT : CLS : PROC_FIGHTHUD
 3550 ENDPROC
 3560 :
 3570 DEF PROC_FIGHTREWARD
 3580 FIGHTREWARD% = 200+RND(800)
 3590 CASH% = CASH% + FIGHTREWARD%
 3600 PROC_FIGHTSTATUSLEFT("YOU FOUND " + STR$(FIGHTREWARD%) + " DOLLARS ON OFFICER HARDASS' CARCAS !!!")
 3610 PROC_BEAT : PROC_BEAT : REM LET IT SINK IN
 3620 CLS
 3630 IF DAMAGE% > 0 THEN PROC_DOCTOR
 3640 ENDPROC
 3650 :
 3660 REM YOU ONLY GET TO SEE THE DOCTOR IF YOU KILL ALL THE COPS
 3670 DEF PROC_DOCTOR
 3680 LOCAL FACTOR, PRICE
 3690 FACTOR% = 200+RND(800)
 3700 PRICE% = DAMAGE%*FACTOR%
 3710 MESSAGE$ = "WILL YOU PAY "+STR$(PRICE%)+" DOLLARS TO HAVE A DOCTOR SEW YOU UP ?"
 3720 PROC_RENDERQUESTION(MESSAGE$)
 3730 LOCAL ANSWER
 3740 ANSWER = FALSE
 3750 ANSWER = FN_DO_I_HEAR_YES
 3760 IF ANSWER = TRUE THEN PROC_SEWYOUUP(PRICE%)
 3770 ENDPROC
 3780 :
 3790 DEF PROC_SEWYOUUP(PRICE%)
 3800 IF CASH% >= PRICE% THEN CASH% = CASH% - PRICE% : DAMAGE% = 0
 3810 ENDPROC
 3820 :
 3830 DEF PROC_RANDOMEVENT
 3840 PROC_HIDEPRICES
 3850 PROC_CLEARQUESTION
 3860 EVENT%=RND(15)
 3870 REM 1-2 NOTHING
 3880 IF EVENT% = 3  THEN PROC_MUGGED
 3890 IF EVENT% = 4  THEN PROC_COKEBUST
 3900 IF EVENT% = 5  THEN PROC_WEEDBOTTOMOUT
 3910 IF EVENT% = 6  THEN PROC_POLICEDOGS
 3920 IF EVENT% = 7  THEN PROC_BROWNIES
 3930 IF EVENT% = 8  THEN PROC_CHEAPHEROIN
 3940 IF EVENT% = 9  THEN PROC_FINDDRUGS
 3950 IF EVENT% = 10 THEN PROC_PARAQUAT
 3960 IF EVENT% = 11 THEN PROC_CHEAPLUDES
 3970 IF EVENT% = 12 THEN PROC_CHEAPACID
 3980 IF EVENT% = 13 THEN PROC_GUNSALE
 3990 IF EVENT% = 14 THEN PROC_COATSALE
 4000 IF EVENT% = 15 THEN PROC_HEROINADDICTS
 4010 ENDPROC
 4020 :
 4030 DEF PROC_MUGGED
 4040 PROC_DISPLAYEVENT("YOU WERE MUGGED ON THE SUBWAY !!")
 4050 CASH% = CASH%*4/5
 4060 PROC_WAITFORSPACE
 4070 ENDPROC
 4080 :
 4090 DEF PROC_COKEBUST
 4100 PROC_DISPLAYEVENT("COPS MADE A BIG COKE BUST !!  PRICES ARE OUTRAGEOUS !!")
 4110 CURRENTPRICES%(COCAINE)=CURRENTPRICES%(COCAINE)*6
 4120 PROC_WAITFORSPACE
 4130 ENDPROC
 4140 :
 4150 DEF PROC_WEEDBOTTOMOUT
 4160 PROC_DISPLAYEVENT("COLOMBIAN FREIGHTER DUSTED THE COAST GUARD !!")
 4170 PROC_DISPLAYEVENTTWO("WEED PRICES HAVE BOTTOMED OUT !!")
 4180 CURRENTPRICES%(WEED)=CURRENTPRICES%(WEED)/5
 4190 PROC_WAITFORSPACE
 4200 ENDPROC
 4210 :
 4220 DEF PROC_POLICEDOGS
 4230 IF FN_TOTALHOLDING > 70 THEN PROC_DOGCHASE
 4240 ENDPROC
 4250 :
 4260 DEF PROC_DOGCHASE
 4270 BLOCKS% = 1+RND(4)
 4280 MESSAGE$ = "POLICE DOGS CHASE YOU "+STR$(BLOCKS%)+" BLOCKS !!"
 4290 PROC_DISPLAYEVENT(MESSAGE$)
 4300 FOR DRUG% = 0 TO 5
 4310   HOLDING%(DRUG%)=HOLDING%(DRUG%)/BLOCKS%
 4320 NEXT DRUG%
 4330 PROC_WAITANYKEY("YOU DROPPED SOME DRUGS !!  THAT'S A DRAG MAN !!")
 4340 ENDPROC
 4350 :
 4360 DEF PROC_BROWNIES
 4370 IF HOLDING%(WEED) > 1 THEN PROC_GETBAKED
 4380 ENDPROC
 4390 :
 4400 DEF PROC_GETBAKED
 4410 COOKED% = 1+RND(4)
 4420 HOLDING%(WEED) = HOLDING%(WEED)/COOKED%
 4430 PROC_DISPLAYEVENT("YOUR MAMA MADE SOME BROWNIES AND USED YOUR WEED !!")
 4440 PROC_BEAT
 4450 PROC_WAITANYKEY("THEY WERE GREAT !!")
 4460 ENDPROC
 4470 :
 4480 DEF PROC_CHEAPHEROIN
 4490 PROC_DISPLAYEVENT("PIGS ARE SELLING CHEAP HEROIN FROM LAST WEEKS RAID !!")
 4500 CURRENTPRICES%(HEROIN)=CURRENTPRICES%(HEROIN)/6
 4510 PROC_WAITFORSPACE
 4520 ENDPROC
 4530 :
 4540 DEF PROC_FINDDRUGS
 4550 IF FN_TOTALHOLDING < 30 THEN PROC_DRUGDISCOVERY
 4560 ENDPROC
 4570 :
 4580 DEF PROC_DRUGDISCOVERY
 4590 QUANTITY% = RND(15)
 4600 IF QUANTITY% > FN_HOLDCAPACITY THEN QUANTITY% = FN_HOLDCAPACITY
 4610 TYPE%=RND(6)-1
 4620 HOLDING%(TYPE%) = HOLDING%(TYPE%)+QUANTITY%
 4630 MESSAGE$="YOU FIND "+STR$(QUANTITY%)+ " UNITS OF "+DRUGNAMES$(TYPE%)+" ON A DEAD DUDE "
 4640 IF TYPE% <> COCAINE AND TYPE% <> HEROIN THEN MESSAGE$=MESSAGE$+ "ON THE SUBWAY "
 4650 MESSAGE$ = MESSAGE$+ "!"
 4660 IF QUANTITY% > 0 THEN PROC_WAITANYKEY(MESSAGE$)
 4670 ENDPROC
 4680 :
 4690 DEF PROC_PARAQUAT
 4700 PROC_DISPLAYEVENT("THERE IS SOME WEED THAT SMELLS LIKE PARAQUAT HERE !!")
 4710 PROC_FURTHERQUESTION("IT LOOKS GOOD !! WILL YOU SMOKE IT?")
 4720 LOCAL ANSWER
 4730 ANSWER = FALSE
 4740 ANSWER = FN_DO_I_HEAR_YES
 4750 IF ANSWER = TRUE THEN PROC_SMOKEIT
 4760 ENDPROC
 4770 :
 4780 DEF PROC_SMOKEIT
 4790 PROC_FURTHERQUESTION(" ") : REM CLEAR LINE
 4800 PROC_DISPLAYEVENT("YOU HALLUCINATE FOR THREE DAYS ON THE WILDEST TRIP !!!")
 4810 PROC_BEAT
 4820 PROC_DISPLAYEVENTTWO("THEN YOU DIE BECAUSE YOUR BRAIN HAS DISINTEGRATED !!!")
 4830 PROC_BEAT : PROC_BEAT : PROC_BEAT : PROC_BEAT
 4840 DAMAGE% = 100
 4850 ENDPROC
 4860 :
 4870 DEF PROC_CHEAPLUDES
 4880 PROC_DISPLAYEVENT("RIVAL DRUG DEALERS RAIDED A PHARMACY AND ARE SELLING")
 4890 PROC_DISPLAYEVENTTWO("              C H E A P   L U D E S !!!")
 4900 CURRENTPRICES%(LUDES)=CURRENTPRICES%(LUDES)/6
 4910 PROC_WAITFORSPACE
 4920 ENDPROC
 4930 :
 4940 DEF PROC_CHEAPACID
 4950 PROC_DISPLAYEVENT("THE MARKET HAS BEEN FLOODED WITH CHEAP HOME MADE ACID !!")
 4960 CURRENTPRICES%(ACID)=CURRENTPRICES%(ACID)/10
 4970 PROC_WAITFORSPACE
 4980 ENDPROC
 4990 :
 5000 DEF PROC_GUNSALE
 5010 GUNFORSALE$ = GUNS$(RND(3))
 5020 GUNPRICE% = 250+RND(250)
 5030 QUESTION$ = "WILL YOU BUY A " + GUNFORSALE$ + " FOR " + STR$(GUNPRICE%) + "? "
 5040 PROC_DISPLAYEVENT(QUESTION$)
 5050 LOCAL ANSWER
 5060 ANSWER = FALSE
 5070 ANSWER = FN_DO_I_HEAR_YES
 5080 IF ANSWER = TRUE THEN PROC_BUYGUN(GUNPRICE%)
 5090 ENDPROC
 5100 :
 5110 DEF PROC_BUYGUN(PRICE%)
 5120 IF CASH% > PRICE% THEN CASH% = CASH%-PRICE% : GUNS% = GUNS%+1
 5130 ENDPROC
 5140 :
 5150 DEF PROC_COATSALE
 5160 COATPRICE% = 150+RND(250)
 5170 QUESTION$ = "WILL YOU BUY A NEW TRENCHCOAT WITH MORE POCKETS FOR " + STR$(COATPRICE%) + "? "
 5180 PROC_DISPLAYEVENT(QUESTION$)
 5190 LOCAL ANSWER
 5200 ANSWER = FALSE
 5210 ANSWER = FN_DO_I_HEAR_YES
 5220 IF ANSWER = TRUE THEN PROC_BUYCOAT(COATPRICE%)
 5230 ENDPROC
 5240 :
 5250 DEF PROC_BUYCOAT(PRICE%)
 5260 CASH% = CASH% - PRICE%
 5270 HOLD% = HOLD% + 5 + RND(35)
 5280 ENDPROC
 5290 :
 5300 DEF PROC_HEROINADDICTS
 5310 PROC_DISPLAYEVENT("ADDICTS ARE BUYING HEROIN AT OUTRAGEOUS PRICES!!")
 5320 CURRENTPRICES%(HEROIN)=CURRENTPRICES%(HEROIN)*6
 5330 PROC_WAITFORSPACE
 5340 ENDPROC
 5350 :
 5360 REM ASK Y/N QUESTION. DOES THE USER SAY YES?
 5370 DEF FN_DO_I_HEAR_YES
 5380 LOCAL ANSWER, KEY%
 5390 ANSWER = FALSE
 5400 KEY% = -1
 5410 REPEAT
 5420   KEY%=INKEY(10000)
 5430 UNTIL KEY% <> -1 AND (KEY% = ASC("Y")) OR (KEY% = ASC("y") OR KEY% = ASC("N")) OR (KEY% = ASC("n"))
 5440 IF (KEY% = ASC("Y")) OR (KEY% = ASC("y")) THEN ANSWER = TRUE
 5450 = ANSWER
 5460 :
 5470 REM Converts keypress into one of the drug constants
 5480 REM Returns number between 0 and 5 if true
 5490 DEF FN_DESIREDDRUG(KEY%)
 5500 DESIREDDRUG% = -1
 5510 IF (KEY% = ASC("C")) OR (KEY% = ASC("c")) THEN DESIREDDRUG% = COCAINE
 5520 IF (KEY% = ASC("H")) OR (KEY% = ASC("h")) THEN DESIREDDRUG% = HEROIN
 5530 IF (KEY% = ASC("A")) OR (KEY% = ASC("a")) THEN DESIREDDRUG% = ACID
 5540 IF (KEY% = ASC("W")) OR (KEY% = ASC("w")) THEN DESIREDDRUG% = WEED
 5550 IF (KEY% = ASC("S")) OR (KEY% = ASC("s")) THEN DESIREDDRUG% = SPEED
 5560 IF (KEY% = ASC("L")) OR (KEY% = ASC("l")) THEN DESIREDDRUG% = LUDES
 5570 = DESIREDDRUG%
 5580 :
 5590 DEF PROC_RENDERUI
 5600 CLS
 5610 PROC_HUD
 5620 PROC_SHOWPRICES
 5630 ENDPROC
 5640 :
 5650 DEF PROC_HUD
 5660 PRINT TAB(LEFT%,2);"DAY ";
 5670 PRINT TAB(LEFT%+10,2);DAY%
 5680 PRINT TAB(RIGHT%,2);"HOLD";
 5690 PRINT TAB(RIGHT%+10,2);FN_HOLDCAPACITY
 5700 PRINT TAB(LEFT%,5);"STASH"
 5710 FOR DRUG% = 0 TO 5
 5720   PRINT TAB(LEFT%,5+2+DRUG%);DRUGNAMES$(DRUG%);
 5730   PRINT TAB(LEFT%+10,5+2+DRUG%);STASH%(DRUG%)
 5740 NEXT DRUG%
 5750 PRINT TAB(MID%,2);LOCATIONS$(GEO%)
 5760 PRINT TAB(RIGHT%,5);"TRENCHCOAT "
 5770 FOR DRUG% = 0 TO 5
 5780   PRINT TAB(RIGHT%,5+2+DRUG%);DRUGNAMES$(DRUG%);
 5790   PRINT TAB(RIGHT%+10,5+2+DRUG%);HOLDING%(DRUG%)
 5800 NEXT DRUG%
 5810 PRINT TAB(LEFT%,14);"BANK";
 5820 PRINT TAB(LEFT%+10,14);BANK%
 5830 PRINT TAB(RIGHT%,14);"GUNS";
 5840 PRINT TAB(RIGHT%+10,14);GUNS%
 5850 PRINT TAB(LEFT%,15);"DEBT";
 5860 PRINT TAB(LEFT%+10,15);DEBT%
 5870 PRINT TAB(RIGHT%,15);"CASH";
 5880 PRINT TAB(RIGHT%+10,15);CASH%
 5890 ENDPROC
 5900 :
 5910 DEF PROC_SHOWPRICES
 5920 PRINT TAB(LEFT%,17);"HEY DUDE, THE PRICES OF DRUGS HERE ARE:"
 5930 PRINT TAB(LEFT%+5,19);"COCAINE";
 5940 PRINT TAB(LEFT%+5+10,19);CURRENTPRICES%(COCAINE)
 5950 PRINT TAB(LEFT%+5,20);"HEROIN";
 5960 PRINT TAB(LEFT%+5+10,20);CURRENTPRICES%(HEROIN)
 5970 PRINT TAB(LEFT%+5,21);"ACID";
 5980 PRINT TAB(LEFT%+5+10,21);CURRENTPRICES%(ACID)
 5990 PRINT TAB(RIGHT%-5,19);"WEED";
 6000 PRINT TAB(RIGHT%-5+10,19);CURRENTPRICES%(WEED)
 6010 PRINT TAB(RIGHT%-5,20);"SPEED";
 6020 PRINT TAB(RIGHT%-5+10,20);CURRENTPRICES%(SPEED)
 6030 PRINT TAB(RIGHT%-5,21);"LUDES";
 6040 PRINT TAB(RIGHT%-5+10,21);CURRENTPRICES%(LUDES)
 6050 ENDPROC
 6060 :
 6070 REM USED TO BLANK PRICES PART OF SCREEN
 6080 DEF PROC_HIDEPRICES
 6090 CLS
 6100 PROC_HUD
 6110 ENDPROC
 6120 :
 6130 DEF PROC_CLEARQUESTION
 6140 REM FIND BETTER WAY TO DO THIS
 6150 PRINT TAB(LEFT%,23);"                                                             "
 6160 ENDPROC
 6170 :
 6180 DEF PROC_RENDERQUESTION(QUESTION$)
 6190 PRINT TAB(LEFT%,23);QUESTION$;
 6200 ENDPROC
 6210 :
 6220 DEF PROC_FURTHERQUESTION(QUESTION$)
 6230 PRINT TAB(LEFT%,25);"                                                             "
 6240 PRINT TAB(LEFT%,25);QUESTION$;
 6250 ENDPROC
 6260 :
 6270 DEF PROC_FURTHERQUESTIONEXTRA(QUESTION$)
 6280 PRINT TAB(LEFT%,26);"                                                             "
 6290 PRINT TAB(LEFT%,26);QUESTION$;
 6300 ENDPROC
 6310 :
 6320 DEF PROC_FIGHTHUD
 6330 COLOUR 128+MAINTEXTCOLOUR%
 6340 PRINT TAB(LEFT%,16);"                                                 "
 6350 COLOUR 128+BACKGROUNDCOLOUR%
 6360 PRINT TAB(LEFT%,18);"DAMAGE : ";DAMAGE%
 6370 PRINT TAB(27,18);"COPS: ";COPS%
 6380 PRINT TAB(RIGHT%,18);"GUNS: ";GUNS%
 6390 COLOUR 128+MAINTEXTCOLOUR%
 6400 PRINT TAB(LEFT%,20);"                                                 "
 6410 COLOUR 128+BACKGROUNDCOLOUR%
 6420 ENDPROC
 6430 :
 6440 DEF PROC_FIGHTQUESTION(MESSAGE$)
 6450 PRINT TAB(LEFT%,22);MESSAGE$;
 6460 ENDPROC
 6470 :
 6480 DEF PROC_FIGHTSTATUSLEFT(MESSAGE$)
 6490 PRINT TAB(LEFT%,24);MESSAGE$
 6500 ENDPROC
 6510 :
 6520 DEF PROC_FIGHTSTATUSRIGHT(MESSAGE$)
 6530 PRINT TAB(36,24);MESSAGE$
 6540 ENDPROC
 6550 :
 6560 DEF PROC_DISPLAYEVENT(MESSAGE$)
 6570 PRINT TAB(LEFT%,22);MESSAGE$;
 6580 ENDPROC
 6590 :
 6600 DEF PROC_DISPLAYEVENTTWO(MESSAGE$)
 6610 PRINT TAB(LEFT%,23);MESSAGE$;
 6620 ENDPROC
 6630 :
 6640 DEF PROC_WAITFORSPACE
 6650 LOCAL KEY%
 6660 KEY% = -1
 6670 PRINT TAB(23,25) "(PRESS SPACE BAR)";
 6680 REPEAT
 6690   KEY%=INKEY(10000)
 6700 UNTIL KEY% <> -1 AND KEY% = 32
 6710 ENDPROC
 6720 :
 6730 DEF PROC_WAITANYKEY(MESSAGE$)
 6740 PRINT TAB(LEFT%,25) MESSAGE$;
 6750 REPEAT
 6760   KEY%=INKEY(10000)
 6770 UNTIL KEY% <> -1
 6780 ENDPROC
 6790 :
 6800 DEF PROC_INITSCREEN
 6810 MODE 1
 6820 COLOUR 6
 6830 CLS
 6840 CLG
 6850 PROC_HIDE_CURSOR
 6860 ENDPROC
 6870 :
 6880 DEF PROC_HIDE_CURSOR
 6890 VDU 23,1,0;0;0;0;
 6900 ENDPROC
 6910 :
 6920 DEF PROC_SHOW_CURSOR
 6930 VDU 23,1,1;0;0;0;
 6940 ENDPROC
 6950 :
 6960 DEF PROC_TITLESPLASH
 6970 PRINT : PRINT : COLOUR 2
 6980 PRINT " 88888888o.      8 888888888o.  8 8888      88    ,o888888o.    "
 6990 PRINT " 888    `^888.   8 8888    `88. 8 8888      88   8888     `88.  "
 7000 PRINT "  88        `88. 8 8888     `88 8 8888      88 8 8888       `8. "
 7010 PRINT "  88         `88 8 8888     ,88 8 8888      88 8 8888           "
 7020 PRINT "  88          88 8 8888.   ,88' 8 8888      88 8 8888           "
 7030 PRINT "  88          88 8 888888888P'  8 8888      88 8 8888           "
 7040 PRINT "  88         ,88 8 8888`8b      8 8888      88 8 8888   8888888 "
 7050 PRINT "  88        ,88' 8 8888 `8b.    ` 8888     ,8P`8 8888       .8' "
 7060 PRINT " 888    ,o88P'   8 8888   `8b.    8888   ,d8P    8888     ,88'  "
 7070 PRINT " 88888888P'      8 8888     `88.   `Y88888P'      `8888888P'    "
 7080 PRINT
 7090 PRINT " 8`888b                 ,8' .8.       888888888o.   d888888o.   "
 7100 PRINT "  8`888b               ,8' .888.      8888    `88..`8888:' `88. "
 7110 PRINT "   8`888b             ,8' :88888.     8888     `888.`8888.   Y8 "
 7120 PRINT "    8`888b     .b    ,8' . `88888.    8888     ,88`8.`8888.     "
 7130 PRINT "     8`888b    88b  ,8' .8. `88888.   8888.   ,88' `8.`8888.    "
 7140 PRINT "      8`888b .`888b,8' .8`8. `88888.  888888888P'   `8.`8888.   "
 7150 PRINT "       8`888b8.`8888' .8' `8. `88888. 8888`8b        `8.`8888.  "
 7160 PRINT "        8`888`8.`88' .8'   `8. `88888.8888 `8b.  8b   `8.`8888. "
 7170 PRINT "         8`8' `8,`' .888888888. `888888888   `8b.`8b.  ;8.`8888 "
 7180 PRINT "          8`   `8' .8'       `8. `88888888     `88`Y8888P ,88P' "
 7190 PRINT : COLOUR MAINTEXTCOLOUR%
 7200 PRINT " A GAME BASED ON THE NEW YORK DRUG MARKET (C) JOHN E DELL (1984)"
 7210 PRINT "      AGON LIGHT VERSION BY OLDPATIENTSEA (C) TIM DELMARE (2023)"
 7220 ANYKEY%=INKEY(400)
 7230 CLS
 7240 ENDPROC
 7250 :
 7260 DEF PROC_FBIWARNING
 7270 PRINT
 7280 PRINT TAB(16,20);"    WINNERS DON'T USE DRUGS"
 7290 PRINT TAB(16,22);"William S Sessions, Director, FBI"
 7300 PRINT
 7310 ANYKEY%=INKEY(300)
 7320 CLS
 7330 ENDPROC
 7340 :
 7350 DEF PROC_OFFERINSTRUCTIONS
 7360 PRINT TAB(17,20)"DO YOU WANT INSTRUCTIONS (Y-N)";
 7370 LOCAL ANSWER
 7380 ANSWER = FALSE
 7390 ANSWER = FN_DO_I_HEAR_YES
 7400 IF ANSWER = TRUE THEN PROC_INSTRUCTIONS
 7410 CLS
 7420 ENDPROC
 7430 :
 7440 DEF PROC_INSTRUCTIONS
 7450 CLS
 7460 PRINT : PRINT : PRINT : PRINT : PRINT : PRINT
 7470 PRINT TAB(12);"This is a game of buying, selling, and   "
 7480 PRINT TAB(12);"fighting. The object of the game is to   "
 7490 PRINT TAB(12);"pay off your debt to the loan shark.     "
 7500 PRINT TAB(12);"Then, make as much money as you can in a "
 7510 PRINT TAB(12);"1 month period. If you deal too heavily  "
 7520 PRINT TAB(12);"in  drugs,  you  might  run  into  the   "
 7530 PRINT TAB(12);"police !!  Your main drug stash will be  "
 7540 PRINT TAB(12);"in the Bronx. (It's a nice neighborhood) "
 7550 PRINT TAB(12);"The prices of drugs per unit are:        "
 7560 PRINT : COLOUR INSTRUCTIONPRICESCOLOUR%
 7570 PRINT TAB(20);"COCAINE     15000-30000                  "
 7580 PRINT TAB(20);"HEROIN      5000-14000                   "
 7590 PRINT TAB(20);"ACID        1000-4500                    "
 7600 PRINT TAB(20);"WEED        300-900                      "
 7610 PRINT TAB(20);"SPEED       70-250                       "
 7620 PRINT TAB(20);"LUDES       10-60                        "
 7630 PRINT : COLOUR MAINTEXTCOLOUR%
 7640 PRINT TAB(16);"(HIT ANY KEY TO START GAME)";
 7650 REPEAT
 7660   ANYKEY%=INKEY(10000)
 7670 UNTIL ANYKEY% <> -1
 7680 CLS
 7690 ENDPROC
 7700 :
 7710 REM CALLED EACH TIME YOU JET AND AT START OF GAME
 7720 REM IF YOU CHANGE THE RANGES REMEMBER TO CHANGE
 7730 REM THE INSTRUCTIONS TOO THAT LIST THEM
 7740 DEF PROC_SETCURRENTPRICES
 7750 CURRENTPRICES%(COCAINE) = 15000+RND(15000)
 7760 CURRENTPRICES%(HEROIN)  =  5000+RND(9000)
 7770 CURRENTPRICES%(ACID)    =  1000+RND(3500)
 7780 CURRENTPRICES%(WEED)    =   300+RND(600)
 7790 CURRENTPRICES%(SPEED)   =    70+RND(180)
 7800 CURRENTPRICES%(LUDES)   =    10+RND(50)
 7810 ENDPROC
 7820 :
 7830 DEF PROC_INIT
 7840 DAY%=1
 7850 JUSTARRIVED = TRUE
 7860 GEO%=1 : REM BRONX
 7870 CASH%=2000
 7880 GUNS%=0
 7890 BANK%=0
 7900 DEBT%=5500
 7910 MAXLOAN%=9450
 7920 HOLD%=100
 7930 STASH%(COCAINE)= 0
 7940 STASH%(HEROIN) = 0
 7950 STASH%(ACID)   = 0
 7960 STASH%(WEED)   = 0
 7970 STASH%(SPEED)  = 0
 7980 STASH%(LUDES)  = 0
 7990 HOLDING%(COCAINE)= 0
 8000 HOLDING%(HEROIN) = 0
 8010 HOLDING%(ACID)   = 0
 8020 HOLDING%(WEED)   = 0
 8030 HOLDING%(SPEED)  = 0
 8040 HOLDING%(LUDES)  = 0
 8050 DAMAGE%=0
 8060 COPS%=0
 8070 ENDPROC
 8080 :
 8090 DEF PROC_BEAT
 8100 REM WAIT HALF A SECOND
 8110 REM THIS WORKS IN ALL MODES WHEREAS THE *FX VERSION REQUIRES YOU TO CHECK MODE
 8120 T = TIME: REPEAT UNTIL (TIME-T) > 100
 8130 ENDPROC
 8140 :
 8150 DEF PROC_CONSTANTS
 8160 REM CONSTANTS USED FOR TAB STOPS WHEN PRINTING
 8170 LEFT% = 6 : MID% = 26 : RIGHT% = 44
 8180 BACKGROUNDCOLOUR% = 16
 8190 MAINTEXTCOLOUR% = 6
 8200 TITLECOLOUR% = 2
 8210 INSTRUCTIONPRICESCOLOUR% = 1
 8220 REM CONSTANTS USED TO INDEX DRUGS ARRAYS
 8230 COCAINE = 0
 8240 HEROIN  = 1
 8250 ACID    = 2
 8260 WEED    = 3
 8270 SPEED   = 4
 8280 LUDES   = 5
 8290 DIM DRUGNAMES$(6)
 8300 DRUGNAMES$(COCAINE) = "COCAINE"
 8310 DRUGNAMES$(HEROIN)  = "HEROIN"
 8320 DRUGNAMES$(ACID)    = "ACID"
 8330 DRUGNAMES$(WEED)    = "WEED"
 8340 DRUGNAMES$(SPEED)   = "SPEED"
 8350 DRUGNAMES$(LUDES)   = "LUDES"
 8360 DIM LOCATIONS$(6)
 8370 LOCATIONS$(1) = "BRONX"
 8380 LOCATIONS$(2) = "GHETTO"
 8390 LOCATIONS$(3) = "CENTRAL PARK"
 8400 LOCATIONS$(4) = "MANHATTAN"
 8410 LOCATIONS$(5) = "CONEY ISLAND"
 8420 LOCATIONS$(6) = "BROOKLYN"
 8430 DIM GUNS$(3)
 8440 GUNS$(1) = "RUGER"
 8450 GUNS$(2) = ".38 SPECIAL"
 8460 GUNS$(3) = "BARETTA"
 8470 REM SET IN CALL TO SETCURRENTPRICES
 8480 DIM CURRENTPRICES%(6)
 8490 REM DRUGS IN STASH
 8500 DIM STASH%(6)
 8510 REM DRUGS HOLDING IN POCKETS OF MY TRENCHCOAT
 8520 DIM HOLDING%(6)
 8530 ENDPROC
 8540 :
 8550 DEF PROC_RESTOREPREGAMESETTINGS
 8560 REM TODO CACHE AND RESTORE MODE ONCE 1.04 RELEASED
 8570 REM      sysvar_scrMode :EQU27h; 1: Screen mode (from MOS 1.04)
 8580 PROC_SHOW_CURSOR
 8590 COLOUR 7
 8600 CLS
 8610 ENDPROC
 8620 :
 8630 REM HANDLE ERROR (WHICH INCLUDES ESCAPE KEY)
 8640 VDU 23,1,1;0;0;0;
 8650 COLOUR 7
 8660 REPORT
 8670 PRINT" at line ";ERL
